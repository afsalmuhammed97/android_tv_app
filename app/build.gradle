apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = "1.8"
  }

  defaultConfig {
    applicationId "com.raywenderlich.android.netderlix"
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def youtubeApiKey = properties.getProperty("youtube.key")

    debug {
      buildConfigField("String", "YOUTUBE_API_KEY", "\"" + youtubeApiKey + "\"")
    }

    release {
      buildConfigField("String", "YOUTUBE_API_KEY", "\"" + youtubeApiKey + "\"")
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}



dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  // Kotlin
  implementation 'androidx.core:core-ktx:1.3.2'
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.31"
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'
  implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'

  // Support Libraries
  implementation 'androidx.appcompat:appcompat:1.2.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
  implementation 'androidx.leanback:leanback:1.0.0'
  implementation 'com.github.bumptech.glide:glide:4.12.0'
  annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'

  // Embedded Youtube player
  implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:10.0.5'

  implementation 'com.google.android.gms:play-services-auth:19.0.0'
  implementation('com.google.api-client:google-api-client-android:1.22.0') {
    exclude group: 'org.apache.httpcomponents'
  }
  implementation('com.google.apis:google-api-services-youtube:v3-rev183-1.22.0') {
    exclude group: 'org.apache.httpcomponents'
  }

  implementation 'com.squareup.retrofit2:retrofit:2.9.0'

}
